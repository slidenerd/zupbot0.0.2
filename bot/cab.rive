! array cabwords = cab taxi
! array cabservices = ola uber

//NO SOURCE NO DESTINATION
//cabwords
//ask source
//ask destination
//ask which cab

//NO SOURCE NO DESTINATION

+ [*] (@cabwords) [*] {weight=996}
- {@int askcabprovider}{topic=bookcab}<set cabdestination=undefined>

+ [*] (@cabservices) [*] {weight=996}
- {@int askcabsource}<set cabprovider=<star>>{topic=bookcab}<set cabdestination=undefined>

+ [*] (@cabwords) [*] (@cabservices) [*] {weight=996}
- {@int askcabsource}<set cabprovider=<star2>>{topic=bookcab}<set cabdestination=undefined>

+ [*] (@cabservices) [*] (@cabwords) [*] {weight=996}
- {@int askcabsource}<set cabprovider=<star1>>{topic=bookcab}<set cabdestination=undefined>

+ [*] (@cabservices) [*] (@cabwords) [*] (to go to|go to|to reach|reach|to) *{weight=996}
- {@int askcabsource}<set cabprovider=<star1>><set cabdestination=<star4>>{topic=bookcab}

> topic bookcab
    + int askcabprovider
    - <call>askCabProvider</call>

    + int askcabprovidermessage
    - Which cab do you prefer?

    + int askcabsource
    - <call>askGeolocation</call>   

    + int askcabsourcemessage
    - Can you please share your location?

    + int handlegeolocation
    * <get cabdestination> != undefined => {@int bookcab}
    - Where would you like to go?

    //While calling @bookcab, if you add a response before it, modify server.js reply method accordingly
    + [*] (to go to|to reach|reach|to) *
    % where would you like to go
    - <set cabdestination=<star2>>{@int bookcab}

    + *
    % where would you like to go
    - <set cabdestination=<star>>{@int bookcab}

    + int bookcab
    - <call>bookCab</call>

    + *
    - <@>{topic=random}
< topic

